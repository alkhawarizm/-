											بســــــــــــــــــــــــم الله الحــــــــــــكيم الخـــــــــــــــــــبير
	
											(۩	
												جميع الحقوق محفوظة © 2019	الخوارزم   Copyright  alkhawarizm.org   
												مرخصة بموجب رخصة أباتشي ، الإصدار 2.0 ("الترخيص") ؛
											 لا يجوز لك استخدام هذا الملف إلا وفقًا للترخيص. يمكنك الحصول على نسخة من الترخيص في

											 https://www.apache.org/licenses/LICENSE-2.0

											 ما لم يكن مطلوبًا بموجب القانون المعمول به أو المتفق عليه كتابيًا أو  يتم توزيع البرنامج بموجب الترخيص على أساس "كما هي" ،
											دون أي ضمانات أو شروط من أي نوع ، سواء كانت صريحة أو ضمنية.  انظر الترخيص للأذونات التي تحكم لغة معينة و القيود بموجب الترخيص.
												۩)												

											
الرزمة	امثلة.شبكة؛

استعمل			 الخوارزم.وسائط.شاشة؛
استعمل 			الخوارزم.اتصالات.شبكة؛				

الصنف  خادم  {
		 
				النوع مشارك شيء {
						نشط منطقي؛	
						مقبس		شبكة::مقبس_متصل		؛ 
						نظير	 شبكة::عنوان_شبكة	؛			
						اسم		[257] حرف؛
					}؛

					المتغير مت شبكة::مقبس_متصل  := عدم؛
					المتغير المقابس  شبكة::مجموعة_مقبس  := عدم؛
					المتغير مشاركين 		[10]  مشارك  ؛

					(۩۩۩		معالجة العميل	۩۩)
					الدالة الخادم_معالجة_العميل(رقم_العميل صحيح) {
						
						المتغير معلومة [1024] حرف :=""؛	
						المتغير عميل صحيح	:= 0؛
						
				(۩		شاشة::اكتب("معالجة العمييل : ")؛	شاشة::اكتب(رقم_العميل)؛شاشة::اكتب("/س")		؛	۩)
						اذا 	شبكة::تلقي(مشاركين[رقم_العميل].مقبس، معلومة، 1024) <= 0  {
							شاشة::اكتب("اقفال المقبس  : ")؛	شاشة::اكتب(رقم_العميل)؛شاشة::اكتب("/س")		؛
							اذا 	مشاركين[رقم_العميل].نشط {
								مشاركين[رقم_العميل].نشط := خطا؛
								شبكة::حذف_مقبس		(المقابس  ، مشاركين[رقم_العميل].مقبس )؛								
								شبكة::اغلاق		(مشاركين[رقم_العميل].مقبس)؛	
								مشاركين[رقم_العميل].مقبس			:= عدم؛					
							}
						} والا {		(۩		اخطار جميع العملاء	۩)
							لاجل عميل  := 0؛ عميل < 10؛ عميل++ { 
								اذا مشاركين[عميل].نشط 	و عميل ليس= رقم_العميل   {
									
								شاشة::اكتب("اخطار العميل  : ")؛	شاشة::اكتب(رقم_العميل)؛شاشة::اكتب("/س")			
								شاشة::اكتب("بالمعلومة  : ")؛	شاشة::اكتب(معلومة)؛شاشة::اكتب("/س")													
								شبكة::ارسال	(مشاركين[عميل].مقبس،   معلومة، 1024 )
								
								}
							}
						}
					}							

					الدالة الخادم_معالجة_الخادم() {

						المتغير متخ 		شبكة::مقبس_متصل := عدم؛	
								المتغير عميل		صحيح16 طبيعي := 0؛

								متخ := شبكة::قبول_مقبس	(مت)؛	

								اذا متخ = عدم {
								(۩	شاشة::اكتب("لااحد/س")؛			۩)
											ارجع؛
								} والا {
									شاشة::اكتب("قبول/س")؛
								}


								لاجل عميل  := 0؛ عميل < 10_ط16؛ عميل++ { 
									اذا مشاركين[عميل].مقبس =  عدم {
										قطع؛
									}
								}
								
								مشاركين[عميل].مقبس := متخ؛
										مشاركين[عميل].نشط := صواب؛										
								مشاركين[عميل].نظير :=  ۢ شبكة::حصل_عنوان_النظير(متخ)؛				
								شاشة::اكتب("اضافة المشارك ")؛	شاشة::اكتب(عميل)؛		شاشة::اكتب("/س")؛
										شبكة::اضافة_مقبس(المقابس، مشاركين[عميل].مقبس )؛
										
					}		

					
					الدالة الخادم() {
						
						المتغير ع صحيح طبيعي := 0؛		
						المتغير متخ 		شبكة::مقبس_متصل := عدم؛	
						المتغير عش شبكة::عنوان_شبكة ؛

						شاشة::اكتب("الخادم/س")؛	

						مادام ع < 10_ط {
							مشاركين[ع كما صحيح].نشط := خطا؛	
									مشاركين[ع كما صحيح].مقبس:= عدم؛						
									ع++؛
						}
(۩
						اذا شبكة::تهيأة(0) = -1  {
							شاشة::اكتب("خطا عند تهيئة المكتبة")؛
						}۩)
						اذا شبكة::تهيأة_التواصل() < 0 {
							شاشة::اكتب("خطا عند تهيئة الاتصال")؛	
									شبكة::نهاية_التواصل()؛					
						}		
						المقابس := 	شبكة::تخصيص_مجموعة_مقابس(10)؛
						اذا المقابس	= عدم {
							شاشة::اكتب("لا يمكن صنع تشكيلة المقابس")؛
						}
						اذا شبكة::حل_المضيف(؏عش، عدم، 1234) < 0  {
							شاشة::اكتب("لا يمكن ايجاد المضيف")؛	
									ارجع ؛					
						}		
						شاشة::اكتب("-- المضيف : ")؛شاشة::اكتب(عش.مضيف كما صحيح)؛شاشة::اكتب("/س")؛
						شاشة::اكتب("المنفذ : ")؛شاشة::اكتب(عش.منفذ)؛شاشة::اكتب("/س")؛
							مت := شبكة::افتح_مقبس(؏عش)؛

						اذا مت = عدم {
							شاشة::اكتب("الخادم:  لا يمكن فتح المقبس المتصل/س")؛	
									ارجع ؛		
						}
							شبكة::اضافة_مقبس(المقابس، مت )؛
							شاشة::اكتب("الخادم:  اضافة_مقبس/س")؛

							مادام صواب {
								اذا 	شبكة::تحقق_من_المقابس(المقابس،500_ط32) < 0 {
									شاشة::اكتب("الخادم: خطا  تحقق_من_المقابس/س")؛	
											قطع؛												
								}
								(۩		انتظار عميل : 	۩)
								شاشة::اكتب("الخادم: انتظر العميل/س")؛											
												اذا		شبكة::مقبس_جاهز(مت كما شبكة::مقبس_شامل)  {
									شاشة::اكتب("الخادم: معالجة_الخادم/س")؛	
											الخادم_معالجة_الخادم()؛																			

								}
							
								شاشة::اكتب("الخادم: انتظر الخادم_معالجة_العميل/س")؛	
								لاجل ع:= 0؛ ع < 10_ط؛ع++ {
									اذا 	مشاركين[ع كما صحيح].مقبس ليس=عدم	{
										شاشة::اكتب(شبكة::مقبس_جاهز(مشاركين[ع كما صحيح].مقبس    كما شبكة::مقبس_شامل))؛	
									}
																	
										اذا 		شبكة::مقبس_جاهز(مشاركين[ع كما صحيح].مقبس    كما شبكة::مقبس_شامل)  {										
											شاشة::اكتب("الخادم:	الخادم_معالجة_العميل : ")؛			شاشة::اكتب(ع)؛شاشة::اكتب("/س")؛		
											الخادم_معالجة_العميل(ع)؛
									}																
								}	
				
						}
						شبكة::نهاية_التواصل()؛			
					}		
					
	(۩۩ 	الاصــــــــــــــــــــــــــــــــــــــــــــــــــــــــل	۩۩)
		الدالة الاصل() {
				(۩	اطلاق الخادم	۩)		
				الخادم()؛	
	}
	
}		
		

											
