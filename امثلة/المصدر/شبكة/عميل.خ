														بسم الله الحكيم الخبير
														(۩	
															جميع الحقوق محفوظة © 2019	الخوارزم   Copyright  alkhawarizm.org   
															مرخصة بموجب رخصة أباتشي ، الإصدار 2.0 ("الترخيص") ؛
														 لا يجوز لك استخدام هذا الملف إلا وفقًا للترخيص. يمكنك الحصول على نسخة من الترخيص في

														 https://www.apache.org/licenses/LICENSE-2.0

														 ما لم يكن مطلوبًا بموجب القانون المعمول به أو المتفق عليه كتابيًا أو  يتم توزيع البرنامج بموجب الترخيص على أساس "كما هي" ،
														دون أي ضمانات أو شروط من أي نوع ، سواء كانت صريحة أو ضمنية.  انظر الترخيص للأذونات التي تحكم لغة معينة و القيود بموجب الترخيص.
															۩)	
الرزمة	امثلة.شبكة

		 
		استعمل			 الخوارزم.تشكيلة.نصيات		
		استعمل 			الخوارزم.اتصالات.شبكة		
		استعمل			 الخوارزم.وسائط.شاشة	

		الصنف  عميل  {

					المتغير 	 مت شبكة::مقبس_متصل  := عدم
					المتغير	 المقابس  شبكة::مجموعة_مقبس  := عدم
					المتغير			عنوان_الشبكة ۢحرف := عدم
					المتغير			اسم_العميل ۢحرف := عدم					
					
					الدالة العميل_معالجة_الخادم()	منطقي {
								المتغير معلومة [1024] حرف :=""	
								المتغير عميل صحيح16 طبيعي := 0
				 				
									
								اذا 	شبكة::تلقي(مت، معلومة، 1024) <= 0  {
											شبكة::حذف_مقبس		(المقابس  ، مت )								
											شبكة::اغلاق		(مت)	
											مت			:= عدم	
											شاشة::اكتب("تم قطع الاتصال بالخادم /س")		
										ارجع خطا											
								} والا {	
													شاشة::خاصية_النص(شاشة::لون_ازرق_ساطع)	
																		شاشة::خاصية_النص(شاشة::لون_عكسي)	
																		شاشة::اكتب("۝ 	")
																		شاشة::اكتب(معلومة)											
																		شاشة::اكتب("			/س")										
																		شاشة::خاصية_النص(شاشة::اعادة_الوضع_السابق)					
																
											ارجع صواب													
								}
					}		

					
		الدالة العميل(كواجهة منطقي) {

						المتغير عش شبكة::عنوان_شبكة 		
						المتغير ن [1024]حرف := ""	
						المتغير معلومة	نصيات::نص	

						اذا شبكة::تهيأة_التواصل() < 0 {
							شاشة::اكتب("خطا عند تهيئة الاتصال")	
									شبكة::نهاية_التواصل()					
						}		
					
						اذا شبكة::حل_المضيف(؏عش،عنوان_الشبكة ، 1234ص16 ) < 0  {
							شاشة::اكتب("لا يمكن ايجاد المضيف ب العنوان : ")	
									شاشة::اكتب(عنوان_الشبكة)									
									ارجع 					
						}
						
								مت := 	شبكة::افتح_مقبس(؏عش)		

						اذا مت = عدم {
							شاشة::اكتب("لا يمكن ايجاد المضيف ب العنوان : ")	
							شاشة::اكتب(عنوان_الشبكة)			
							ارجع									
							
						} والا {
							المقابس := 	شبكة::تخصيص_مجموعة_مقابس(2)
							اذا المقابس	= عدم {
								شاشة::اكتب("لا يمكن صنع تشكيلة المقابس")
										ارجع											
							}							
							شبكة::اضافة_مقبس(المقابس، مت )
																							
							مادام صواب {
							شبكة::تحقق_من_المقابس(المقابس،50ط)
							
								اذا ليس كواجهة {
									لافتة ()									
										واجهة_الحوار(اسم_العميل) 	
										معلومة := 	نصيات::جديد(اسم_العميل).وصل(	نصيات::جديد("	: ")).وصل(نصيات::اقرا())	
										شبكة::ارسال	(مت	، معلومة.استخرج() ،   1024)	
								}
									مادام شبكة::مقبس_جاهز(مت    كما شبكة::مقبس_شامل) {
										اذا ليس 	العميل_معالجة_الخادم() {
													ارجع									
										}											
									}	
							}
						}
						
						شبكة::نهاية_التواصل()
					}
		
		
		(۩۩	رسم الواجهة	۩۩)

			الدالة واجهة_الحوار(نص_الحوار ۢحرف)			 {
				شاشة::خاصية_النص(شاشة::لون_ازرق_ساطع)	
									شاشة::خاصية_النص(شاشة::لون_عكسي)	
									شاشة::انتقل_الى	(10، 10)	
									شاشة::اكتب("۝ 	")
									شاشة::اكتب(نص_الحوار)											
									شاشة::اكتب("			:")										
									شاشة::خاصية_النص(شاشة::اعادة_الوضع_السابق)		
		}			
		
		الدالة لافتة () 			{
			شاشة::مسح()							
					شاشة::خاصية_النص(شاشة::لون_اخضر_ساطع)	
					شاشة::انتقل_الى	(2، 30)					
					شاشة::اكتب("۝	۞۞۞۞۞۞	السلام عليكم	۞۞۞۞۞۞۞	۝/س")	
			}			
		

		الدالة اطلاق_العميل(عدد صحيح، عامل   ۢ ۢ حرف جامد) 			{
			المتغير كواجهة منطقي := خطا

			اذا عدد < 3 {
				شاشة::اكتب(" المرجو ادخال اسم العميل او الكلمة 'واجهة' متبوعا بعنوان شبكة الخادم/س ")
				شاشة::اكتب("  عميل	[اسم العميل او 'واجهة'] 	[رقم.رقم.رقم.رقم]	/س ")						
				ارجع					
			}
			اسم_العميل := عامل[1]
							   كواجهة  :=   نصيات::مطابق("واجهة"،اسم_العميل)			
			عنوان_الشبكة := عامل[2]
								 				  
										 شاشة::تمكين_المعالجة_الافتراضية()
			لافتة()	
			العميل(كواجهة) 
		} 
		
	(۩۩	 	الاصــــــــــــــــــــــــــــــــــــــــــــــــــــــــل	 
 	الدالة الاصل(عدد صحيح، عامل   ۢ ۢ حرف جامد) 			{
 		 اطلاق_العميل(عدد ، عامل  ) 	

	}		۩۩)
 }
